import "../decent/common.eol";

("Running trace2decent").println();
var Model = DECENT!Model.allInstances().first();

var attributes = new Map;
attributes.put("BugCount","Number of linked issue reports");
attributes.put("BugComments","Number of comments in linked issue reports");
attributes.put("BugEvents","Number of events related to linked issue reports");
attributes.put("BugResolutions","Number of resolutions of linked issue reports");
attributes.put("BugLifespan","Lifespan of linked issue )ports");
Model.addAttributes(attributes);

var BugCountAttribute = DECENT!Attribute.allInstances().select(t|t.name = "BugCount").first();
var BugCommentsAttribute = DECENT!Attribute.allInstances().select(t|t.name = "BugComments").first();
var BugEventsAttribute = DECENT!Attribute.allInstances().select(t|t.name = "BugEvents").first();
var BugResolutionsAttribute = DECENT!Attribute.allInstances().select(t|t.name = "BugResolutions").first();
var BugLifespanAttribute = DECENT!Attribute.allInstances().select(t|t.name = "BugLifespan").first();

var artifactTypes = new OrderedSet("build","code","documentation","ui","image","unknown","devel-doc");

//TODO: filter for files only?
for (s in DECENT!State.allInstances().select(s|artifactTypes.includes(s.artifact.type.name))) {
	for (a in attributes) {
		s.values.removeAll(s.values.select(v|v.ofAttribute.name = a));
	}

	var bugCount = 0.asDouble();
	var commentCount = 0.asDouble();
	var eventCount = 0.asDouble();
	var resolutionsCount = 0.asDouble();
	var lifespan = 0;
	for (t in TRACE!Trace.allInstances().select(x|x.target.commit_id = s.ID)) {
		//TODO:what if there are more than one?
		// => currently summed together
		bugCount = bugCount + 1;
		commentCount = commentCount + t.source.comments.size().asDouble();
		eventCount = eventCount + t.source.events.size().asDouble();
		resolutionsCount = resolutionsCount + t.source.events.select(x|x.newValue = "FIXED").size().asDouble();
		lifespan = lifespan + (t.source.lastModifiedOn.time - t.source.reportedOn.time);
		("  Issue "+t.source.issueId+" mapped to State "+s.ID+ " of " +s.artifact.name+" : "+resolutionsCount + " / " + eventCount + " / "+commentCount).println();
	}
	s.addValue(BugCountAttribute, bugCount);
	s.addValue(BugCommentsAttribute, commentCount);
	s.addValue(BugEventsAttribute, eventCount);
	s.addValue(BugResolutionsAttribute, resolutionsCount);
	s.addValue(BugLifespanAttribute, lifespan.toDays());
	//TODO: add lifetime, involved people, etc.
}

Model.content.add("trace2decent3");

