import "../decent/common.eol";

"Running hits2decent ".println();

//TODO: could also be integrated back into famix2decent as inline checking 
//to further filter out unchanged elements 

var Model = DECENT!Model.allInstances().first();

var attributes = new OrderedSet;
attributes.add("LinesPostHits");
attributes.add("StartLine");
attributes.add("EndLine");
  
for (a in attributes) {
    if (Model.attributePool.attributes.select(att|att.name = a).size() = 0) {
		var Attribute = new DECENT!Attribute;
		Attribute.name = a;
	    Model.attributePool.attributes.add(Attribute);
    }
}

var ClassArtifactType = DECENT!ArtifactType.allInstances().select(t|t.name = "Class").first();
var MethodArtifactType = DECENT!ArtifactType.allInstances().select(t|t.name = "Method").first();
var FunctionArtifactType = DECENT!ArtifactType.allInstances().select(t|t.name = "Function").first();
var ModuleArtifactType = DECENT!ArtifactType.allInstances().select(t|t.name = "Module").first();
var StartLineAttribute = DECENT!Attribute.allInstances().select(a|a.name = "StartLine").first();
var EndLineAttribute = DECENT!Attribute.allInstances().select(a|a.name = "EndLine").first();
var LinesPreAttribute = DECENT!Attribute.allInstances().select(a|a.name = "LinesPre").first();
var LinesPostAttribute = DECENT!Attribute.allInstances().select(a|a.name = "LinesPost").first();
var LinesPostHitsAttribute = DECENT!Attribute.allInstances().select(a|a.name = "LinesPostHits").first();

var existingStatesInRevision = DECENT!State.allInstances().select(s|
									s.artifact.type = ClassArtifactType or 
  									s.artifact.type = MethodArtifactType or 
  									s.artifact.type = ModuleArtifactType or 
  									s.artifact.type = FunctionArtifactType);
for (s in existingStatesInRevision) {
	var startLine = s.values.select(v|v.ofAttribute = StartLineAttribute).first().content;
	var endLine = s.values.select(v|v.ofAttribute = EndLineAttribute).first().content;
	var parentState = s.artifact.parent.states.select(p|p.ID = s.ID).first();
	var linesPost = parentState.values.select(v|v.ofAttribute = LinesPostAttribute).first().content;
	var linesPostHits = linesPost.select(l|l >= startLine and l <= endLine);
/*
	("  "+s.artifact.name+"@"+s.ID+":"+startLine+ ":"+endLine).println();
	("    "+parentState.artifact.name+"@"+parentState.ID+":"+startLine+ ":"+endLine).println();
	("      "+linesPost).println();
	("        "+linesPostHits).println();
*/	
	s.values.removeAll(s.values.select(v|v.ofAttribute = LinesPostHitsAttribute));
	s.addIntegerCollectionValue(LinesPostHitsAttribute, linesPostHits);
}

Model.content.add("hits2decent3");
