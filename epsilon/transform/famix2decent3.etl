import "../libraries/decent/common.eol";
import "../libraries/decent/logging.eol";


pre {
  //Some initialization
  var revisionId = FAMIX!AnchoredElement.allInstances().first().revisionId;
  
  var step = "famix2decent3 @ "+revisionId; 
  ("Running "+step).log(1);
  
  var start = Native("java.lang.System").currentTimeMillis();
  
  ("DECENT States: "+DECENT!State.allInstances().size()).log(2);
  var Model = DECENT!Model.allInstances().first();
  
  var ClassArtifactType = DECENT!ArtifactType.allInstances().select(t|t.name = "Class").first();
  var MethodArtifactType = DECENT!ArtifactType.allInstances().select(t|t.name = "Method").first();
  var FunctionArtifactType = DECENT!ArtifactType.allInstances().select(t|t.name = "Function").first();
  var ModuleArtifactType = DECENT!ArtifactType.allInstances().select(t|t.name = "Module").first();

  var StartLineAttribute = addAttribute("StartLine", "Artifact start line");
  var EndLineAttribute = addAttribute("EndLine", "Artifact end line");
  
  var LinesPostHitsAttribute = "LinesPostHits".getAttribute();
  var FileAnchorAttribute = "FileAnchor".getAttribute();
  var FilePathAttribute = "FilePath".getAttribute();
  
  var revisionId = -1;
  var potentiallyAffectedClasses = new OrderedSet;
  var potentiallyAffectedMethods = new OrderedSet;
  
  var codeStatesInRevision = new OrderedSet;
  var cleaned = false;
  var profiler : new Native("org.eclipse.epsilon.eol.tools.ProfilerTool");
   
  if (FAMIX!FileAnchor.allInstances().size() > 0) {
	  revisionId = FAMIX!FileAnchor.allInstances().first().revisionId;
	  codeStatesInRevision = revisionId.getCodeStatesForID();
	  ("DECENT Code States in "+revisionId + ": "+codeStatesInRevision.size()).log(2);
	  for (s in codeStatesInRevision) {
	    ("  Changed state of Code artifact " + s.values.select(v|v.ofAttribute = FilePathAttribute).first().content).log(3);
	  }

	  potentiallyAffectedClasses = FAMIX!Class.getPotentiallyAffectedAnchoredElements();
	  potentiallyAffectedMethods = FAMIX!Method.getPotentiallyAffectedAnchoredElements();
	  ("FAMIX Classes in "+revisionId + ": "+potentiallyAffectedClasses.size()+"/"+FAMIX!Class.allInstances().size()).log(2);
	  ("FAMIX Methods in "+revisionId + ": "+potentiallyAffectedMethods.size()+"/"+FAMIX!Class.allInstances().size()).log(2);
	
	  var existingStatesInRevision = revisionId.getFAMIXStatesForID();
	  ("Existing DECENT Anchored Element States in "+revisionId + ": "+existingStatesInRevision.size()).log(2);

	  var fromActivities = new OrderedSet;
	  
	  //TODO: this is a potentially faulty approach...
	  for (s in existingStatesInRevision) {
	  	cleaned = true;
	    ("  Cleaning up existing state " + s.ID +" of artifact " + s.artifact.name + " ("+s.artifact.type.name+")").log(3);
	    for (a in s.fromActivity) {
	    	fromActivities.add(a);
	    }
	  	delete s;
	  }
	  for (a in fromActivities) {
	  	delete a;
	  }
  	  //TODO: consider generating separate decent instances for each revision (or a set of revisinos) which may be 
  	  //handled as a resource set
  }
  profiler.start("all");

}

post {
	profiler.stop();
	("Updating broken activities...").log(2);
	("DECENT Code States in "+revisionId + ": "+codeStatesInRevision.size()).log(2);
	
	//TODO: this may be a faulty approach
	//top down approach potentially inefficient
	if (cleaned) { 
	    //for all states of affected code artifacts
	    for (cs in codeStatesInRevision) {
			("  State of "+cs.artifact.name + " at " + cs.ID).log(3);
		    //for all next states of an affected code artifact
		  	for (next in cs.next) {
		  		("    Next state of "+next.artifact.name + " at " + next.ID).log(4);
		  		//for all children of affected code artifact 
		  		for (child in cs.artifact.children) {
		  			("      Child of "+next.artifact.name + " - " + child.name).log(4);
		  			//get newly added state of child
		  			for (current in child.states.select(s|s.ID = revisionId).first()) {
		  				if (current.isDefined()) {
			  				("        State of "+current.artifact.name + " at " + current.ID).log(4);
				  			//for all states of the child of the affected artifact corresponding to the next state of that artifact
				  			for (s in child.states.select(s|s.ID = next.ID)) {
				  				("          Next state of "+s.artifact.name + " at " + s.ID).log(4);
				  				s.previous.add(current);
				  				//update broken activities
							    for (a in s.fromActivity.select(a|a.state = null)) {
							    	("            Fixing activity on "+current.ID +" to "+s.ID + " of "+s.artifact.name).log(4);
							    	a.state = current;
							    	for (d in a.deltas) {
							    		d.sourceValue = current.values.select(v|v.ofAttribute = d.onAttribute).first();
							    	}
							    }
				  			}
		  				}
		  			}
		  			
		  		}
	    	}
		}
	}
	//TODO: add hits labeling as a post processing step (with an option for on-the-fly filering as a guard)
	//- add affected lines in decent (from mg)
	//- startLine = s.values.select(v|v.ofAttribute = StartLineAttribute).first().content
	//- endLine = s.values.select(v|v.ofAttribute = EndLineAttribute).first().content
	//or 
	//- startLine = e.anchor.startLine
	//- endLine = e.anchor.endLine
	//then
	//- select(l|l >= startLine and l <= endLine)
	var end = Native("java.lang.System").currentTimeMillis();
    var duration = end - start;
	Model.content.add(step);
  	Model.addStep(step,duration.toMinutes().round(5));
    ("Duration: "+duration.toMinutes().round(5)).log(1);
	
}

operation Any getPotentiallyAffectedAnchoredElements() : Sequence {
	return self.allInstances.select(c|c.sourceAnchor_IN_FileAnchor.fileName <> "_unknown_path" and c.sourceAnchor_IN_FileAnchor.fileName.checkParentArtifactStateExists());
}

@cached
operation String getParentArtifactStates() : Collection {
	return codeStatesInRevision.getMatchingStateForPath(self);
}


@cached
operation String checkParentArtifactStateExists() : Boolean {
	var count = self.getParentArtifactStates().size();
	//("---- "+count+" states for artifacts located in "+filePath+" at "+stateId).log(1);
	return count > 0;
}

operation DECENT!State mapAnchoredElement(c : FAMIX!AnchoredElement, artifactType : DECENT!ArtifactType, name : String) {
    var fileName = c.sourceAnchor_IN_FileAnchor.fileName;
    ("Transforming "+artifactType.name+" " +name +" at revision "+c.revisionId+" located in "+fileName).log(3);
    var candidateParentStates = fileName.getParentArtifactStates();
    //TODO: what if more than one?
    var parentArtifact = candidateParentStates.first().artifact;
    self.parent = candidateParentStates.first();
    var candidateArtifacts = parentArtifact.children.select(child|child.name = name and child.type = artifactType);
    var artifact = null;
    if (candidateArtifacts.size() > 0) {
        //TODO: what if more than one?
        artifact = candidateArtifacts.first();
    } else {
        artifact = new DECENT!Artifact;
        artifact.name = name;
        artifact.type = artifactType;
        artifact.parent = parentArtifact;
    }

    var lastState = null; 
    if (artifact.states.size() > 0) {
    	//TODO: what if more than one?
    	var parentPrevious = self.parent.previous.first();
    	if (parentPrevious.isDefined()) {
	    	lastState = artifact.states.select(s|s.ID = parentPrevious.ID).first();
	        //lastState = artifact.states.last();
	        if (lastState.isDefined()) {
		        self.previous.add(lastState);
	        } 
    	}
    }

    self.name = self.parent.name;
    self.ID = c.revisionId;
    artifact.states.add(self);

    self.addValue(FileAnchorAttribute, c.sourceAnchor_IN_FileAnchor.fileName+":"+c.sourceAnchor_IN_FileAnchor.startLine+":"+c.sourceAnchor_IN_FileAnchor.endLine);
    self.addValue(StartLineAttribute, c.sourceAnchor_IN_FileAnchor.startLine);
    self.addValue(EndLineAttribute, c.sourceAnchor_IN_FileAnchor.endLine);

    //TODO: keep in mind the parent states may be more than one
    var activity = new DECENT!Activity;
    activity.state = lastState;            
    activity.targetState = self;
    //TODO: need to properly select corresponding parent activity, this is only the naive approach
    var parentArtifactActivity = self.parent.fromActivity.first();
    activity.date = parentArtifactActivity.date;
    activity.type = parentArtifactActivity.type;
    activity.agent = parentArtifactActivity.agent;
    
    for (attr in c.eClass().getSelectedAttributes()) {
        var attribute = attr.getName().getAttribute(); 
        if (not attribute.isDefined()){
        	attribute = addAttribute(attr.getName(),"See FAMIX attributes.");
            attribute.artifactTypes.add(artifactType);
        }
        self.addValue(attribute, c.eGet(attr));
    }
}

@cached
operation String isSelectedAttribute() : Boolean {
	return self == "EDouble" or self == "Boolean";
}

@cached
operation FAMIX!EClass getSelectedAttributes() : Collection {
	return self.getEAllAttributes().select(x|x.getEAttributeType().getName().isSelectedAttribute());
}

rule Class2State
    transform c : FAMIX!Class 
    to s : DECENT!State {
    
    guard : c.sourceAnchor_IN_FileAnchor.fileName <> "_unknown_path" and c.sourceAnchor_IN_FileAnchor.fileName.checkParentArtifactStateExists()

    s.mapAnchoredElement(c, ClassArtifactType, c.name);
    c.~done = true;
    (FAMIX!Class.allInstances().select(c|c.~done == true).size()+"/"+potentiallyAffectedClasses.size() + " done").log(1);

}

rule Module2State 
    transform m : FAMIX!Module 
    to s : DECENT!State {
    guard {
        var parentPackageName = m.parentPackage_IN_Package.first().name;
        var modulePath = m.name;
        if (parentPackageName.length() > 0) {
            modulePath = parentPackageName.substring(1)+"/"+m.name;
        }
        return m.name <> "_unknown_file" and modulePath.checkParentArtifactStateExists(); 
    }

    //create a temparary anchor
    var anchor : new FAMIX!FileAnchor;
    anchor.revisionId = m.revisionId;

    var parentPackageName = m.parentPackage_IN_Package.first().name;
    var modulePath = m.name;
    if (parentPackageName.length() > 0) {
        modulePath = parentPackageName.substring(1)+"/"+m.name;
    }

    anchor.fileName = modulePath;
    //TODO: add start and endlines from file
    anchor.anchoredElement = m;
    s.mapAnchoredElement(m, ModuleArtifactType, m.name);

}


rule Function2State 
    transform f : FAMIX!Function 
    to s : DECENT!State {
    guard : f.sourceAnchor_IN_FileAnchor.fileName <> "_unknown_path" and f.sourceAnchor_IN_FileAnchor.fileName.checkParentArtifactStateExists()

    s.mapAnchoredElement(f, FunctionArtifactType, f.signature);

    //TODO: with regard to methods and functions, these need to be filtered based on whether they are affected by an activity 
    //(via hunks/hits algorithm)
}

rule Method2State 
    transform m : FAMIX!Method 
    to s : DECENT!State {
    //SOOOOOOOOOO..... class definitions are typically within h files, while method definitinos are within cpp files
    //the whole situation is a lot more messed up than that, but this is the gist of it
    //which makes the case for direct artifact containment next to impossible
    //instead a different relationship will be necessary
    //as for transformation, it should be based solely on file anchors and then this relationship should be reestablished


    //TODO: also note that if the class is attached to the header and the header is not 
    //modified this will essentially break everything since there will be no states even for methods
    //as the containing classes will not be mapped either
    //THUS treat them separately, check for discrepancies and report them (or deal with them somehow) 
    //WHICH also means that the transformations shall be mostly very similar based on the file anchors

    guard : m.sourceAnchor_IN_FileAnchor.fileName <> "_unknown_path" and m.sourceAnchor_IN_FileAnchor.fileName.checkParentArtifactStateExists()

    s.mapAnchoredElement(m, MethodArtifactType, m.parentType_IN_Class.first().name+"::"+m.signature);

    m.~done = true;
    (FAMIX!Method.allInstances().select(m|m.~done == true).size()+"/"+potentiallyAffectedMethods.size() + " done").log(1);   
}

