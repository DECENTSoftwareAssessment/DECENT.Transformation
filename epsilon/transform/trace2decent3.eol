import "../libraries/decent/common.eol";
import "../libraries/decent/logging.eol";

var step = "trace2decent3"; 
("Running "+step).log(1);
var start = Native("java.lang.System").currentTimeMillis();

var Model = DECENT!Model.allInstances().first();

var BugCountAttribute = addAttribute("BugCount","Number of linked issue reports");
var BugCommentsAttribute = addAttribute("BugComments","Number of comments in linked issue reports");
var BugEventsAttribute = addAttribute("BugEvents","Number of events related to linked issue reports");
var BugResolutionsAttribute = addAttribute("BugResolutions","Number of resolutions of linked issue reports");
var BugLifespanAttribute = addAttribute("BugLifespan","Lifespan of linked issue )ports");

var artifactTypes = new OrderedSet("build","code","documentation","ui","image","unknown","devel-doc");

//TODO: filter for files only?
for (s in DECENT!State.allInstances().select(s|artifactTypes.includes(s.artifact.type.name))) {
	var bugCount = 0.asDouble();
	var commentCount = 0.asDouble();
	var eventCount = 0.asDouble();
	var resolutionsCount = 0.asDouble();
	var lifespan = 0;
	for (t in TRACE!Trace.allInstances().select(x|x.target.commit_id = s.ID)) {
		//TODO:what if there are more than one?
		// => currently summed together
		bugCount = bugCount + 1;
		commentCount = commentCount + t.source.comments.size().asDouble();
		eventCount = eventCount + t.source.events.size().asDouble();
		resolutionsCount = resolutionsCount + t.source.events.select(x|x.newValue = "FIXED").size().asDouble();
		lifespan = lifespan + (t.source.lastModifiedOn.time - t.source.reportedOn.time);
		("  Issue "+t.source.issueId+" mapped to State "+s.ID+ " of " +s.artifact.name+" : "+resolutionsCount + " / " + eventCount + " / "+commentCount).log(1);
	}
	s.addValue(BugCountAttribute, bugCount);
	s.addValue(BugCommentsAttribute, commentCount);
	s.addValue(BugEventsAttribute, eventCount);
	s.addValue(BugResolutionsAttribute, resolutionsCount);
	s.addValue(BugLifespanAttribute, lifespan.toDays());
	//TODO: add lifetime, involved people, etc.
}


var end = Native("java.lang.System").currentTimeMillis();
var duration = end - start;
Model.content.add(step);
Model.addStep(step,duration.toMinutes().round(5));
("Duration: "+duration.toMinutes().round(5)).log(1);



