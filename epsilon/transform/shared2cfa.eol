import "../libraries/cfa/common.eol";
import "../libraries/decent/common.eol";
import "../libraries/decent/logging.eol";


  	"Running shared2cfa".log(1);

  	var start = Native("java.lang.System").currentTimeMillis();
	
	var cfn = CFA!CFN.allInstances().first();
	
  	var WeightFactorAttribute = "WeightFactor".getCFAAttribute();
  	var TotalWeightAttribute = "TotalWeight".getCFAAttribute();
  	var AverageWeightAttribute = "AverageWeight".getCFAAttribute();

	var suffix = ".EqualSplit";
  	var factors = new Map;

	var cfaFactors = CFA!Factor.allInstances().select(x|not x.name.endsWith(suffix));
	for (f in cfaFactors) {
	  	factors.put(f.name+suffix, f.description + "(equal split)");
	  	f.~sharedName = f.name+suffix; //for lookup later
	}
	
  	for (a in factors.keySet()) {
  		if (CFA!Factor.allInstances().select(x|x.name = a).size() = 0) {
			var factor = new CFA!Factor;
			factor.name = a;
			factor.description = factors.get(a);
			cfn.factors.add(factor);
		}
  	}

	addFactors();
  	
  	for (a in factors.keySet()) {
  		var f = a.getCFAFactor();
  		resetTotalWeights(f);
		calculateTotalFactorWeights(f);
		calculateAverageFactorWeights();
	}
	
    var end = Native("java.lang.System").currentTimeMillis();
    var duration = end - start;
    ("Duration: "+duration.toMinutes().round(5)).log(1);

operation addFactors() {
	for (s in CFA!GlobalState.allInstances) {
  		var splitCount = 1.asDouble();
  		s.addSharedFactors(splitCount);
	}
	for (s in CFA!ArtifactState.allInstances) {
  		var splitCount = s.globalState.getArtifactSplitCount();
  		s.addSharedFactors(splitCount);
	}
	for (s in CFA!LogicalState.allInstances) {
  		var splitCount = s.globalState.getLogicalSplitCount(s.artifact.type);
  		s.addSharedFactors(splitCount);
	}
}

operation CFA!CFAState addSharedFactors(splitCount : Real) {
  	for (a in factors.keySet()) {
  		var f = a.getCFAFactor();
  		var baseWeightFactor = self.getBaseWeightFactor(a);
		self.addFactor(f, baseWeightFactor/splitCount);
	}
}

@cached
operation CFA!GlobalState getBaseWeightFactor(sharedFactorName : String) : Real {
  	var f = sharedFactorName.getBaseFactor();
	return self.factors.get(f).get(WeightFactorAttribute);
}

operation CFA!ArtifactState getBaseWeightFactor(sharedFactorName : String) : Real {
	return self.globalState.getBaseWeightFactor(sharedFactorName);
}

operation CFA!LogicalState getBaseWeightFactor(sharedFactorName : String) : Real {
	return self.globalState.getBaseWeightFactor(sharedFactorName);
}


@cached
operation CFA!GlobalState getArtifactSplitCount() : Real {
	return self.artifactStates.size().asDouble();
}

//TODO: do we also need a split for artifact/logical.count?
@cached
operation CFA!GlobalState getLogicalSplitCount(type : DECENT!ArtifactType) : Real {
	return self.logicalStates.select(x|x.artifact.type = type).size().asDouble();
}

@cached
operation String getBaseFactor() : CFA!Factor {
	return cfaFactors.select(x|x.~sharedName = self).first();
}
	