import "../libraries/cfa/common.eol";
import "../libraries/decent/common.eol";
import "../libraries/decent/logging.eol";


  	"Running shared2cfa".log(1);

  	var start = Native("java.lang.System").currentTimeMillis();
	
	var cfn = CFA!CFN.allInstances().first();
	
  	var WeightFactorAttribute = "WeightFactor".getCFAAttribute();
  	var TotalWeightAttribute = "TotalWeight".getCFAAttribute();
  	var AverageWeightAttribute = "AverageWeight".getCFAAttribute();

	var suffix = ".EqualSplit";

	var cfaFactors = cfn.factors.select(x|not x.name.endsWith(suffix));
	for (f in cfaFactors) {
	  	f.~sharedName = f.name+suffix;

  		if (CFA!Factor.allInstances().select(x|x.name = f.~sharedName).size() = 0) {
			var factor = new CFA!Factor;
			factor.name = f.~sharedName;
			factor.description = f.description + " (equal split)";
			cfn.factors.add(factor);
		}
  	}

	addFactors();
  	
	for (f in cfaFactors) {
  		var sf = f.~sharedName.getCFAFactor();
  		resetTotalWeights(sf);
		calculateTotalFactorWeights(sf);
		calculateAverageFactorWeights();
	}
	
    var end = Native("java.lang.System").currentTimeMillis();
    var duration = end - start;
    ("Duration: "+duration.toMinutes().round(5)).log(1);

operation addFactors() {
	for (s in CFA!GlobalState.allInstances) {
  		var splitCount = 1.asDouble();
  		s.addSharedFactors(splitCount);
	}
	for (s in CFA!ArtifactState.allInstances) {
  		var splitCount = s.globalState.getArtifactSplitCount();
  		s.addSharedFactors(splitCount);
	}
	for (s in CFA!LogicalState.allInstances) {
  		var splitCount = s.globalState.getLogicalSplitCount(s.artifact.type);
  		s.addSharedFactors(splitCount);
	}
}

operation CFA!CFAState addSharedFactors(splitCount : Real) {
	for (f in cfaFactors) {
  		var sf = f.~sharedName.getCFAFactor();
  		var baseWeightFactor = self.getBaseWeightFactor(f.~sharedName); //TODO: change to factor? 
		self.addFactor(sf, baseWeightFactor/splitCount);
	}
}

@cached
operation CFA!GlobalState getBaseWeightFactor(sharedFactorName : String) : Real {
  	var f = sharedFactorName.getBaseFactor();
	return self.factors.get(f).get(WeightFactorAttribute);
}

operation CFA!ArtifactState getBaseWeightFactor(sharedFactorName : String) : Real {
	return self.globalState.getBaseWeightFactor(sharedFactorName);
}

operation CFA!LogicalState getBaseWeightFactor(sharedFactorName : String) : Real {
	return self.globalState.getBaseWeightFactor(sharedFactorName);
}


@cached
operation CFA!GlobalState getArtifactSplitCount() : Real {
	return self.artifactStates.size().asDouble();
}

//TODO: do we also need a split for artifact/logical.count?
@cached
operation CFA!GlobalState getLogicalSplitCount(type : DECENT!ArtifactType) : Real {
	return self.logicalStates.select(x|x.artifact.type = type).size().asDouble();
}

@cached
operation String getBaseFactor() : CFA!Factor {
	return cfaFactors.select(x|x.~sharedName = self).first();
}
	