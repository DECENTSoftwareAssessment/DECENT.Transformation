import "../libraries/famix/init.etl";
import "../libraries/decent/common.eol";
import "../libraries/decent/logging.eol";

pre mgInitPre {

  //Some initialization
  //More generic (might be simplified)
  //TODO: may be exported to a separate module
  //("System "+System.getContext().getModule()).log(1);
  "Running mg2decent v3".log(1);
  var start = Native("java.lang.System").currentTimeMillis();
  /********************************** INIT (1x) *******************************/
  
  var Model : new DECENT!Model;
  Model.artifactTypeHierarchy = new DECENT!ArtifactTypeHierarchy;
  Model.agentPool = new DECENT!AgentPool;
  Model.attributePool = new DECENT!AttributePool;
  //Model.projects = new List;
  
  var BranchArtifactType = new DECENT!ArtifactType;
  BranchArtifactType.name = "Branch";
  Model.artifactTypeHierarchy.types.add(BranchArtifactType);
    
  var attributes = new Map;
  attributes.put("FilePath","File path");
  attributes.put("Branch","Branch name");
  attributes.put("FileSize","File size in bytes");
  attributes.put("LOC","Lines of code");
  attributes.put("LinesAdded","Lines added");
  attributes.put("LinesRemoved","Lines removed");
  attributes.put("IsBugFix","Revision is labeled as bug fix");
  attributes.put("BugFixCount","Number of bug fixes in past revisions");
  attributes.put("AggregateFragmentCount","");
  attributes.put("FragmentCount","Number of fragments");
  attributes.put("HunkCount","Number of changed fragments");
  attributes.put("ChangedFragments","Changed and unchanged fragments");
  attributes.put("Fragments","Fragments");
  attributes.put("VarianceChangedFragments","Variance of changed and unchanged fragments");
  attributes.put("VarianceFragments","Variance of fragments");
  attributes.put("Message","Commit message");
  attributes.put("LinesPre","Affected lines from the previous state");
  attributes.put("LinesPost","Affected lines in the resulting state");
  attributes.put("LinesMerged","Affected lines from both the previous and the resulting state (merged)");
  attributes.put("VarianceLinesPre","Variance of the affected lines from the previous state");
  attributes.put("VarianceLinesPost","Variance of the affected lines in the resulting state");
  attributes.put("VarianceLinesMerged","Variance of the affected lines from both the previous and the resulting state");

  Model.addAttributes(attributes);

  var FilePathAttribute = "FilePath".getAttribute();
  var BranchAttribute = "Branch".getAttribute();
  var FileSizeAttribute = "FileSize".getAttribute();
  var LOCAttribute = "LOC".getAttribute();
  var LinesAddedAttribute = "LinesAdded".getAttribute();
  var LinesRemovedAttribute = "LinesRemoved".getAttribute();
  var IsBugFixAttribute = "IsBugFix".getAttribute();
  var BugFixCountAttribute = "BugFixCount".getAttribute();
  var AggregateFragmentCountAttribute = "AggregateFragmentCount".getAttribute();
  var FragmentCountAttribute = "FragmentCount".getAttribute();
  var HunkCountAttribute = "HunkCount".getAttribute();
  var ChangedFragmentAttribute = "ChangedFragments".getAttribute();
  var FragmentsAttribute = "Fragments".getAttribute();
  var VarianceChangedFragmentAttribute = "VarianceChangedFragments".getAttribute();
  var VarianceFragmentsAttribute = "VarianceFragments".getAttribute();
  var MessageAttribute = "Message".getAttribute();
  var LinesPreAttribute = "LinesPre".getAttribute();
  var LinesPostAttribute = "LinesPost".getAttribute();
  var LinesMergedAttribute = "LinesMerged".getAttribute();
  var VarianceLinesPreAttribute = "VarianceLinesPre".getAttribute();
  var VarianceLinesPostAttribute = "VarianceLinesPost".getAttribute();
  var VarianceLinesMergedAttribute = "VarianceLinesMerged".getAttribute();

  var Project = new DECENT!Project;
  Project.name = MG!Model.allInstances().first().name;
  Model.projects.add(Project);
      
  //TODO: handle all branches

  var artifactCount = MG!File.allInstances().size();
  var processedCount = 0;
  
  var filterBranches = new Set(); 
  filterBranches.add("master");
}

post mgInitPost{
  ("Post processing... ").log(2);
  //TODO: move to a separate standalone module?
  //stats for debugging...
  for (a in DECENT!Artifact.allInstances()) {
  	(""+a.name).log(3);
  	for (s in a.states) {
  		for (v in s.values.select(v|v.isTypeOf(DoubleValue) or v.isTypeOf(IntegerValue))) {
  			s.fromActivity.get(0).addDelta(v.ofAttribute);
  		}
  	}
  }

    //TODO: adjust branching structure
  Model.content.add("mg2decent3");
  
  var end = Native("java.lang.System").currentTimeMillis();
  var duration = end - start;
  ("Duration: "+duration.toMinutes().round(5)).log(1);
  
}

operation String replaceExtension() : String {
    return self.replace('\\.h','.cpp');
}

operation String replaceInclude() : String {
    return self.replace('include','src');
}

operation dumpFileCopies() {
  for (f in MG!File.allInstances()) {
    (""+f.file_name).log(3);
  
  	for (a in f.actions) {
  		("  " +a.type + " "+a.revision.commit_id + " " + a.revision.message.trim()).log(3);
  		if (a.copy.isDefined()) {
  			("  "+a.copy.fromRevision.commit_id +" ("+a.copy.fromFile.file_name+")" + " -> "+ a.revision.commit_id+ " ("+f.file_name+")").log(3);
  		}
  	}
  }
}

@cached
operation MG!File getBranch() : MG!Branch {
	if (self.file_type = "directory") {
		return self.childrenLinks.first().revision.actions.first().branch;
	} else {
		return self.actions.first().branch;
	}
}

@cached
operation MG!Revision getBranch() : MG!Branch {
	return self.actions.first().branch;
}


rule Branch2Location 
    transform b : MG!Branch 
    to l : DECENT!Location {
    guard : filterBranches.size()=0 or filterBranches.includes(b.name)
    
	l.name = b.name;
    ("Transforming location "+l.name).log(3);
	
	l.type = BranchArtifactType;
	Project.location.add(l);
}


rule People2Agent 
    transform p : MG!People 
    to a : DECENT!Agent {
    
	a.name = p.name;
    ("Transforming agent "+a.name).log(3);
	a.eMail = p.email;
	Model.agentPool.agents.add(a);
	Model.projects.get(0).agents.add(a);
}

//rule-based
rule File2Artifact 
    transform f : MG!File 
    to a : DECENT!Artifact {
    guard : filterBranches.size()=0 or filterBranches.includes(f.getBranch().name)
    
    a.name = f.file_name;
   	a.setArtifactType(f.file_type);
    var parentName = "ROOT";
    if (f.parentLinks.size()>0) {
    	//("Adding parents... "+f.parentLinks.size()).log(1);
    	("Adding parents... "+f.parentLinks.get(0).parent.equivalent().name).log(4);
	    var parentArtifact = f.parentLinks.get(0).parent.equivalent();
	    if (parentArtifact.name = "ROOT") {
	    	f.getBranch().equivalent().artifacts.add(a);
	    } else {
		    a.parent = parentArtifact;
		    parentName = parentArtifact.name;
	    }
    } else {
    	//TODO: handle branches properly
		//f.actions.first().branch.equivalent().rootArtifact=a;
    }
    ("Transforming artifact "+f.file_name+" ("+f.file_type+") in "+parentName+" ("+processedCount+"/"+artifactCount+" completed)").log(3);
    
	processedCount = processedCount + 1;
}

rule Revision2AgentState 
    transform r : MG!Revision
    to s : DECENT!AgentState {
    guard : filterBranches.size()=0 or filterBranches.includes(r.getBranch().name)
    
    var agent = r.author.equivalent(); 
    s.agent = agent;
    ("Transforming revision "+ r.commit_id + " to agent state for " + agent.name).log(3);
    s.ID = r.commit_id;
    s.name = r.rev;
    s.date = r.author_date;
}

rule Action2State 
    transform a : MG!Action
    to s : DECENT!State {
    guard : filterBranches.size()=0 or filterBranches.includes(a.branch.name)

	s.name = a.revision.rev;
    ("Transforming action "+ a.revision.commit_id + " for " + a.file.file_name).log(3);
	s.ID = a.revision.commit_id;
	s.artifact = a.file.equivalent();
	
	s.addValue(FilePathAttribute, a.current_file_path);
	s.addValue(BranchAttribute, a.branch.name);
	
	var lastState = null;
	var aIndex = a.file.actions.indexOf(a);
	if (aIndex > 0) {
		//("  Previous Action "+a.file.actions.at(aIndex-1).revision.commit_id + " mapped to " + a.file.actions.at(aIndex-1).equivalent()).log(1);
		lastState = a.file.actions.at(aIndex-1).equivalent();
		s.previous.add(lastState);
	}
	
    //dealing with file copies
    //DONE: see notes on this
    //if (a.copy.isDefined()){
    //	var copySourceState = a.copy.fromRevision.actions.select(af|af.file = a.copy.fromFile).first().equivalent();
    //	("  Copy from "+ a.copy.fromFile.equivalent() + " in state " + copySourceState).log(1);
    //	s.previous.add(copySourceState);
    //}
    
    //dealing with file moves
    //TODO: peform a small study similar to file copies

    var content = a.revision.content.select(c|c.file = a.file).first();
	if (content.isDefined()){
		s.addValue(FileSizeAttribute, a.revision.content.select(c|c.file = a.file).first().size.asDouble());
		s.addValue(LOCAttribute, a.revision.content.select(c|c.file = a.file).first().loc.asDouble());
	} else {
		s.addValue(FileSizeAttribute, -1.asDouble());
		s.addValue(LOCAttribute, -1.asDouble());
	}

    s.addValue(AggregateFragmentCountAttribute, a.revision.lineBlames->select(l|l.file = a.file).size().asDouble());
	
	s.addSpatialCharacteristics(a.revision.hunks->select(h|h.file = a.file));
	//TODO: add temporal characteristics
	//TODO: also add distances between bugs, own changes, all changes, bug lifetimes..
	
	//TODO: handle activities and deltas (as a post step?)
	//TODO: deltas!
    var activity = new DECENT!Activity;
    activity.targetState = s;
    activity.state = lastState;            
    activity.date = a.revision.author_date;
    activity.setActivityType(a.type);
    activity.agent = a.revision.author.equivalent();
	
    
    var linesAdded = "0".asDouble();
    var linesRemoved = "0".asDouble();
    var difference = "0".asDouble();
    var patchLines = a.revision.patchLines->select(l|l.file = a.file);
    if (patchLines.size() > 0) {
        linesAdded = patchLines.asOrderedSet().first().added.asDouble();
        linesRemoved = patchLines.asOrderedSet().first().removed.asDouble();
        difference = linesAdded - linesRemoved;
    }

    activity.addValue(HunkCountAttribute, a.revision.hunks->select(h|h.file = a.file).size().asDouble());
	activity.addValue(MessageAttribute, a.revision.message);
    activity.addValue(LinesAddedAttribute, linesAdded);
    activity.addValue(LinesRemovedAttribute, linesRemoved);

}


operation DECENT!State addSpatialCharacteristics(hunks : Collection) {
	//consider outsourcing
	var linesPre = new OrderedSet;
	var linesPost = new OrderedSet;
	var linesMerged = new OrderedSet;
	var fragments = new OrderedSet;
	var size = 0;
	var preDifference = 0;
	var postDifference = 0;
	var fragmentCount = 0;
	var tailChanged = false;
    for (h in hunks) {
		var oldStartLine = h.old_start_line_normalized;
		var newStartLine = h.new_start_line_normalized;
		var oldEndLine = h.old_end_line_normalized;
		var newEndLine = h.new_end_line_normalized;
		oldEndLine = oldEndLine + 1;
		newEndLine = newEndLine + 1;
		if (h.old_start_line = 0) {
			oldEndLine = oldEndLine - 1;
		}
		if (h.new_start_line = 0) {
			newEndLine = newEndLine - 1;
		}
		//can be further refined into addAll(Sequence{oldStartLine..(oldEndLine-1)})
		//and addAll(Sequence{oldStartLine..(oldEndLine-1)}.collect(v|v + postDifference)
		var i = 0;
		while (i < (oldEndLine - oldStartLine)){
			linesPre.add(oldStartLine + i);
			linesMerged.add(oldStartLine + i + postDifference);
			i = i + 1;
		}
		i = 0;
		while (i < (newEndLine - newStartLine)){
			linesPost.add(newStartLine + i);
			if (h.old_end_line = 0 ) {
				linesMerged.add(newStartLine + i + preDifference);
			}
			i = i + 1;
		}

		size=size+h.difference;
		
		if (h.difference > 0) {
			postDifference = postDifference + h.difference.abs();
		} 
		if (h.difference < 0) {
			preDifference = preDifference + h.difference.abs();
		}
    	
		if (oldStartLine = 1 or newStartLine = 1) {
		} else {
			fragmentCount = fragmentCount + 1;
			fragments.add(fragmentCount);
		}
		if (oldEndLine-1 = size or newEndLine-1 = size) {
			tailChanged = true;
		}
		fragmentCount = fragmentCount + 1;
		fragments.add(fragmentCount);
    	
    }
	if (not tailChanged) {
		fragmentCount = fragmentCount + 1;
		fragments.add(fragmentCount);
	}
	self.addIntegerCollectionValue(LinesPreAttribute, linesPre.sortBy(l|l));
	self.addIntegerCollectionValue(LinesPostAttribute, linesPost.sortBy(l|l));
	self.addIntegerCollectionValue(LinesMergedAttribute, linesMerged.sortBy(l|l));
	self.addValue(FragmentCountAttribute, fragments.size().asDouble());
	self.addValue(VarianceLinesPreAttribute, linesPre.getVariance());
	self.addValue(VarianceLinesPostAttribute, linesPost.getVariance());
	self.addValue(VarianceLinesMergedAttribute, linesMerged.getVariance());
	self.addIntegerCollectionValue(FragmentsAttribute, fragments.sortBy(l|l));
	self.addValue(VarianceFragmentsAttribute, fragments.getVariance());
	
//  TODO: ChangedFragmentAttribute
//  TODO: VarianceChangedFragmentAttribute
	
	
}

